apply plugin: 'maven-publish'
apply plugin: 'signing'

File localPropFile = project.rootProject.file('local.properties')

if (localPropFile.exists()) {
    println "Found secret props file, loading props"
    Properties p = new Properties()
    p.load(new FileInputStream(localPropFile))
    p.each { name, value ->
        ext[name] = value
    }
} else {
    println "No props file, loading env vars"
    ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID')
    ext["signing.password"] = System.getenv('SIGNING_PASSWORD')
    ext["signing.secretKeyRingFile"] = System.getenv('SIGNING_SECRET_KEY_RING_FILE')
    ext["ossrhUsername"] = System.getenv('OSSRH_USERNAME')
    ext["ossrhPassword"] = System.getenv('OSSRH_PASSWORD')
}

group = GROUP
version = VERSION_NAME

task androidSourcesJar(type: Jar) {
    archiveClassifier.set 'sources'
    from android.sourceSets.main.java.source
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {

                // Applies the component for the release build variant.
                from components.release

                artifact androidSourcesJar

                pom {
                    name = ARTIFACT_ID
                    description = 'A very lightweight android MVVM architecture template.'
                    url = 'https://github.com/myth-china/myth-arch-mvvm-android'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'yan66665'
                            name = 'yanweiqiang'
                            email = 'yan66665@126.com'
                        }
                    }

                    // Version control info, if you're using GitHub, follow the format as seen here
                    scm {
                        connection = 'scm:git@github.com:myth-china/myth-arch-mvvm-android.git'
                        developerConnection = 'scm:git@github.com:myth-china/myth-arch-mvvm-android.git'
                        url = 'https://github.com/myth-china/myth-arch-mvvm-android'
                    }
                }
            }
        }

        repositories {
            // The repository to publish to, Sonatype/MavenCentral
            maven {
                // This is an arbitrary name, you may also use "mavencentral" or
                // any other name that's descriptive for you
                name = "sonatype"

                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"

                // You only need this if you want to publish snapshots, otherwise just set the URL
                // to the release repo directly
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                // The username and password we've fetched earlier
                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
    }

    signing {
        sign publishing.publications
    }
}